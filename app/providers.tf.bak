terraform {
  required_version = ">= 0.15"
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 3.60.0"
    }
    kubernetes = {
      source  = "hashicorp/kubernetes"
      version = "~> 2.5.0"
    }
  }
}

data "aws_caller_identity" "current" {}
data "aws_region" "current" {}

output "whoami" {
  value = data.aws_caller_identity.current.arn
}

###
### aws provider
###
provider "aws" {
  region              = "eu-central-1"
  allowed_account_ids = [731288958074]
  default_tags {
    tags = {
      terraform = true
      workspace = terraform.workspace
    }
  }
  # no assume_role here; credentials are set via environment variables to avoid Terraform+MFA problems
}

provider "kubernetes" {
  host = "all-k8s-resources-must-specify-a-provider"
}
provider "kubernetes" {
  alias                  = "project"
  host                   = module.auth.host
  cluster_ca_certificate = nonsensitive(module.auth.cluster_ca_certificate)
  token                  = module.auth.token
}

module "auth" {
  source      = "../modules/cluster-auth"
  cluster_set = "project"
  suffix      = "apps"
}
provider "kustomization" {
  # This provider is inherited by the various k8s modules which transform their respective yaml manifests into k8s resources.
  kubeconfig_raw = module.cluster.auth.kubeconfig
}
